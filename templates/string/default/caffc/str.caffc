module caffc

native {
  /**
   * strcmp() with CaffC semantics, without a dependency on libC.
   */
  caffc_i32 caffc_strcmp(caffc_u8* left_string, caffc_u8* right_string) {
    while (*left_string && *right_string && *left_string == *right_string) {
      left_string++;
      right_string++;
    }

    return *left_string - *right_string;
  }

  /**
   * indexOf() with CaffC semantics, without a dependency on libC
   */
  caffc_i32 caffc_index_of(caffc_u8* haystack, caffc_i32 haystack_len,
                           caffc_u8* needle, caffc_i32 needle_len) {
    caffc_i32 i, j;

    if (needle_len == 0) return 0;
    if (haystack_len < needle_len) return -1;

    for (i = 0; i <= haystack_len - needle_len; ++i) {
      for (j = 0; j < needle_len; ++j) {
          if (haystack[i + j] != needle[j]) break;
      }
      if (j == needle_len) return i;
    }

    return -1;
  }

  /**
   * memcpy() with CaffC semantics, without a dependency on libC
   */
  void caffc_memcpy(caffc_ptr dest, caffc_ptr src, caffc_i32 size) {
    caffc_u8* destPtr = (caffc_u8*) dest;
    caffc_u8* srcPtr = (caffc_u8*) src;
    caffc_i32 i;

    for (i = 0; i < size; i++) {
      destPtr[i] = srcPtr[i];
    }
  }

  /**
   * strlen() with CaffC semantics, without a dependency on libC
   */
  caffc_i32 caffc_strlen(caffc_ptr data) {
    caffc_i32 result = 0;

    while (*((char*)data)) {
      result += 1;
      data = (caffc_ptr) (((char*)data) + 1);
    }

    return result;
  }
}

_caffc_str_size(i32 size) -> i32 {
  i32 result

  native {
    result = (caffc_i32) (sizeof(caffc_u8) * size + sizeof(caffc_str));
  }

  return result
}

/**
 * A basic string. This implementation only holds ASCII strings.
 */
#public #caffc_array("caffc_u8")
class str {
  i32 _byte_length   // size in bytes, including nul byte. this only holds ASCII strings

  constructor(i32 size) {
    if size <= 0 {
      raise("invalid string size.")
    }

    _this._byte_length = size
  }

  /**
   * Creates a copy of the bytes until the first nul byte and creates a new `str` instance.
   * The `ptr` reference isn't stored internally, and a copy of the string will be made.
   */
  #public
  static fromPtr(ptr data) -> str {
     u32 byte_length
     str result

     native {
       byte_length = caffc_strlen(data) + 1;
     }

     result = new str(byte_length)

     native {
       caffc_memcpy(result->_caffc_data, data, byte_length);
     }

     return result
  }

  /**
   * Concatenates the current `str`, with the other `str`, into a new `str`
   * instance.
   */
  #public
  add(str other) -> str {
    if not other {
      raise("illegal null argument")
    }

    str result = new str(_this._byte_length + other._byte_length - 1)

    native {
      caffc_memcpy(result->_caffc_data, _this->_caffc_data, _this->_byte_length - 1);
      caffc_memcpy(result->_caffc_data + _this->_byte_length - 1,
                   other->_caffc_data, other->_byte_length); /* this copies also the nul byte */
    }

    return result
  }

  /**
   * Slices the current `str` into a new `str`. `start` and `end` positions are
   * character indexes (they can be negative). `0` for the `end_index` means
   * `_this.size()`
   */
  #public
  slice(i32 start_index, i32 end_index) -> str {
      if start_index < 0 {
        start_index = (i32) _this._byte_length + start_index - 1
      }

      if (u32) start_index >= (u32) _this._byte_length {
        raise("start_index out of bounds")
      }

      if end_index <= 0 {
        end_index = (i32) _this._byte_length + end_index - 1
      }

      if (u32) end_index > (u32) _this._byte_length {
        raise("end_index out of bounds")
      }

      if start_index > end_index {
        raise("start_index > end_index")
      }

      str result = new str(end_index - start_index + 1)

      native {
        caffc_memcpy(result->_caffc_data,
                     _this->_caffc_data + start_index, result->_byte_length - 1);
        result->_caffc_data[result->_byte_length - 1] = 0;
      }

      return result
  }

  /**
   * Returns the character at the given character position.
   */
  #public
  get(i32 index) -> u32 {
    if index < 0 {
      index = (i32) _this._byte_length + index
    }

    if (u32) index >= (u32) _this._byte_length {
      raise("index out of bounds")
    }

    u32 result

    native {
      result = (caffc_u32) _this->_caffc_data[index];
    }

    return result
  }

  /**
   * Returns the size of this string in _characters_. It excludes the nul byte,
   * since it's not relevant.
   */
  #public
  size() -> i32 {
    return _this._byte_length - 1
  }

  /**
   * Checks if the current string is exactly the same as the other string.
   */
  #public
  equals(str other) -> bool {
    bool result

    native {
      if (!other) {
        result = 0;
      } else if (_this->_byte_length != other->_byte_length) {
        result = 0;
      } else {
        result = caffc_strcmp(_this->_caffc_data, other->_caffc_data) == 0 ? 1 : 0;
      }
    }

    return result
  }

  /**
   * Returns the index of another string inside the current string if it exists,
   * or `-1` if it doesn't exists.
   */
  #public
  indexOf(str needle) -> i32 {
    if not needle {
      raise("illegal null argument")
    }

    i32 result

    native {
      result = caffc_index_of(_this->_caffc_data, _this->_byte_length - 1,
                              needle->_caffc_data, needle->_byte_length - 1);
    }

    return result
  }

  /**
   * Compares a string to another string for ordering purposes.
   */
  #public
  compareTo(str other) -> i32 {
    if not other {
      raise("illegal null argument")
    }

    i32 result

    native {
      result = caffc_strcmp(_this->_caffc_data, other->_caffc_data);
    }

    return result
  }
}
